plugins {
    id 'org.springframework.boot' version "2.6.3"
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'maven-publish'
}

group = 'com.yfway.base'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding('UTF-8')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

}

ext {
    set('springCloudVersion', "2021.0.1")
    set('springBootVersion', "2.6.3")
}

repositories {
    maven {
        allowInsecureProtocol true
        name 'yfway-nexus'
        url 'http://nexus.yfyun.gtland/yfang/repository/maven-public/'
    }
    mavenLocal()
    mavenCentral()
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        maven {
            allowInsecureProtocol true
            name 'yfway-nexus'
            url 'http://nexus.yfyun.gtland/yfang/repository/maven-public/'
        }
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        implementation('com.yfway.base:yfway-base-utils:1.1.2-SNAPSHOT') {
            exclude group: 'io.springfox', module: 'springfox-boot-starter'
        }
        compileOnly 'org.projectlombok:lombok'
        compileOnly('org.mapstruct:mapstruct:1.5.3.Final')
        compileOnly('org.mapstruct:mapstruct-jdk8:1.5.3.Final')

        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
        annotationProcessor('org.mapstruct:mapstruct-processor:1.5.3.Final')
        annotationProcessor('org.mapstruct:mapstruct-jdk8:1.5.3.Final')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    bootJar {
        enabled(false)
    }

    jar {
        enabled(true)
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }

    println findProperty('MAVEN_USER')

    publishing {

        publications {
            pub(MavenPublication) {
                groupId = this.group
                artifactId = project.name
                version = this.version
                from components.java
                artifact sourcesJar
            }
        }

        repositories {
            maven {
                name 'yfway-nexus'
                allowInsecureProtocol true
                credentials {
                    username findProperty('MAVEN_USERNAME')
                    password findProperty('MAVEN_PASSWORD')
                }
                url = this.version.endsWith('SNAPSHOT')
                        ? 'http://nexus.yfyun.gtland/yfang/repository/maven-snapshots/'
                        : 'http://nexus.yfyun.gtland/yfang/repository/maven-releases/'
            }
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

bootJar {
    enabled(false)
}

jar {
    enabled(true)
}